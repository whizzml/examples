(define MIN-RARE-INSTANCES 7)

;; Transposes the matrix: [[a b c] [d e f]] to [[a d] [b e] [c f]]
(define (_transpose m)
  (apply map list m))

;; Deletes resources ignoring errors
(define (_safe-delete id)
  (try (delete id)
       (catch e
         (log-info (str "Error deleting resource " id " ignored:" e))
         false)))

;; Find objective ID field info
(define (_find-objective fields objective-field)
  (find-field fields objective-field))

;; Returns the instances associated to a class in a distribution
(define (_class-instances field class)
  (let (field-summary (field-distribution field))
    ((head (filter (lambda (f) (= (f 0) class))
                                        field-summary)) 1)))

;; Returns the total instances in a distribution
(define (_total-instances field)
  (let (field-summary (field-distribution field))
    (reduce + 0 (map (lambda (c) (c 1)) field-summary))))

;; Checks positive number
(define (_check-positive number var-name)
  (when (<= number 0)
    (log-info (str var-name " is not a positive number"))
    (raise {"message" "The " var-name " value needs to be a positive number."
            "code" 105})))

;; Checks integer number
(define (_check-integer number var-name)
  (when (not (integer? number))
    (log-info (str var-name " is not an integer number"))
    (raise {"message" "The " var-name " value needs to be an integer."
            "code" 106})))

;; Check that the objective field exists, is a categorical field and
;; contains the rare class
(define (_check-cat fields objective-field rare-class)
  (let (field (_find-objective fields objective-field)
        rare-instances (_class-instances field rare-class))
    (when (not field)
      (raise {"message" (str "The objective field "
                             objective-field
                             " cannot be found in this dataset")
              "code" 100}))
    (when (not (categorical-field? field))
      (raise {"message" (str "The objective field "
                             objective-field
                             " is not a categorical field."
                             " Only categorical fields can be used.")
              "code" 101}))
    (when (not (member? rare-class (field-categories field)))
      (raise {"message" (str "The objective field "
                             objective-field
                             " does not contain the "
                             rare-class
                             " category. Please check its name.")
              "code" 102}))
    (when (< rare-instances MIN-RARE-INSTANCES)
      (raise {"message" (str "The objective field "
                             objective-field
                             " contains "
                             rare-instances
                             " instances of the "
                             rare-class
                             " category. "
                             MIN-RARE-INSTANCES
                             " instances are needed at least.")
              "code" 103})
    (when (= (_total-instances field) rare-instances)
      (raise {"message" (str "The objective field "
                             objective-field
                             " only contains one class.")
              "code" 104})))))

;; Function that splits a dataset in training, validation and test datasets
(define (_create-train-validation-test ds-id train-rate seed)
  (let (first-split (create-dataset-split ds-id train-rate seed)
        second-split (create-dataset-split (last first-split) 0.5 seed))
    (cons (head first-split) second-split)))


;; Function to apply stratified split for the rare class of the objective field
;; with a given train
(define (rare-stratified-split-fn ds-id training-rate objective-field
                                  rare-class project)
  (let (dataset (fetch ds-id)
        ds-name (dataset "name")
        fields (resource-fields dataset)
        _ (_check-cat fields objective-field rare-class)
        objective-id ((_find-objective fields objective-field) "id")
        filters [(flatline "(= (f {{objective-id}}) {{rare-class}})")
                 (flatline "(!= (f {{objective-id}}) {{rare-class}})")]
        names [rare-class (str "not " rare-class)]
        cat-ds-ids-fn (lambda (expr name) (_create-train-validation-test
                        (create-dataset {"origin_dataset" ds-id
                                         "lisp_filter" expr
                                         "name" (str (dataset "name")
                                                     " - "
                                                     name)
                                         "project" project})
                        training-rate
                        "bigml"))
        cat-ds-ids (map cat-ds-ids-fn filters names)
        inner-ds (flatten cat-ds-ids)
        ds-ids (map (lambda (ids name)
                      (merge-datasets ids {"name" name}))
                    (_transpose cat-ds-ids)
                    [(str ds-name " - train")
                     (str ds-name " - validation")
                     (str ds-name " - test")]))
    (wait* ds-ids)
    (map _safe-delete inner-ds)
    ds-ids))


;; Function to generated datasets that keep all instances of the rare class
;; while undersampling the instances in the common class to maintain a
;; proportion with the rare class
(define (undersample-fn ds-id number-of-samples objective-field rare-class
                        proportion)
  (let (dataset (fetch ds-id)
        ds-name (dataset "name")
        fields (resource-fields dataset)
        _ (_check-positive number-of-samples "number-of-samples")
        _ (_check-integer number-of-samples "number-of-samples")
        _ (_check-positive proportion "proportion")
        _ (_check-cat fields objective-field rare-class)
        objective-f (_find-objective fields objective-field)
        objective-id (objective-f "id")
        rows (dataset "rows")
        rare-instances (_class-instances objective-f rare-class)
        common-instances (- rows rare-instances)
        balance-rate (* proportion (/ rare-instances common-instances))
        rare-filter (flatline "(= (f {{objective-id}}) {{rare-class}})")
        common-filter (flatline "(!= (f {{objective-id}}) {{rare-class}})")
        rare-ds (create-dataset {"origin_dataset" ds-id
                                 "lisp_filter" rare-filter
                                 "name" (str (dataset "name")
                                             " - train ["
                                             objective-field
                                             "="
                                             rare-class
                                             "]")})
        samples-ds-ids (iterate (acc [] index (range number-of-samples))
                         (append acc
                           (create-dataset {"origin_dataset" ds-id
                                            "lisp_filter" common-filter
                                            "sample_rate" balance-rate
                                            "name" (str (dataset "name")
                                                        " - "
                                                        index)
                                            "seed" (str "bigml" index)})))
        ds-ids (map (lambda (sampled-ds index)
                      (merge-datasets [sampled-ds rare-ds]
                                      {"name" (str ds-name
                                                   " - undersampled "
                                                   index)}))
                    samples-ds-ids (range number-of-samples)))

    (wait* ds-ids)
    (map _safe-delete samples-ds-ids)
    (_safe-delete rare-ds)
    ds-ids))

;; Function to create a collection of OptiMLs for a particular metric and class
;; using a validation dataset
(define (optimls-fn ds
                    objective-field
                    metric
                    metric-class
                    validation-ds
                    optiml-config)
  (let (config {"dataset" ds
                "objective_field" objective-field
                "test_dataset" validation-ds
                "metric" metric
                "metric_class" metric-class}
        config (merge config optiml-config))
    (create-optiml config)))
